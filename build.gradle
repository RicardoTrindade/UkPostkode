plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'maven-publish'
    id 'org.jetbrains.dokka' version '0.10.1'
}

apply plugin: 'maven'
apply plugin: 'signing'

group 'com.ricardo-trindade'
version '0.0.1'

repositories {
    mavenCentral()
    jcenter()
}

signing {
    sign configurations.archives
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"

}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    testImplementation("junit:junit:4.12")
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from "$buildDir/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Uk Postkode'
                packaging 'jar'
                // optionally artifactId can be defined here
                description "Uk Postcode helper library built in kotlin"
                url "https://github.com/RicardoTrindade/UkPostkode"

                scm {
                    connection 'scm:git:git://github.com/RicardoTrindade/UkPostkode.git'
                    developerConnection 'scm:git:ssh://github.com/RicardoTrindade/UkPostkode.git'
                    url 'http://github.com/RicardoTrindade/UkPostkode/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'RicardoTrindade'
                        name 'Ricardo Trindade'
                        email 'ricardo.trindade743@gmail.com'
                    }
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}